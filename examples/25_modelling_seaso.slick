// (atom A) iff A is within (true _).
atom A if true A.

atom A1 if
    and cons _ A1
    and not same {A1 (_ _)}
    and not same {A1 (_ _ _)}
    and not same {A1 (_ _ _ _)}
    and not same {A1 (_ _ _ _ _)}.

atom A1 and atom A2 if
    cons _ (A1 A2).

atom A1 and atom A2 and atom A3 if
    cons _ (A1 A2 A3).

atom A1 and atom A2 and atom A3 and atom A4 if
    cons _ (A1 A2 A3 A4).

atom A1 and atom A2 and atom A3 and atom A4 and atom A5 if
    cons _ (A1 A2 A3 A4 A5).

// identify all rule constructs
cons Domain Args if atom (Domain Args).

// identify evident types
(Domain Args) : Domain if cons Domain Args. 

// declared types are also defined
decl Domain if defn Domain _.

// types of atoms must be defined
error (undeclared domain Domain)
    if _ : Domain and not decl Domain.

// types of atoms must be unique
error (clashing types Domain1 also Domain2 for atom Atom) if
    and Atom : Domain1
    and Atom : Domain2
    and diff {Domain1 Domain2}.

// type from defn
Args :zip Params if 
    and defn Domain Params
    and cons Domain Args.

A1 : P1 if
    and A1 :zip P1
    and atom A1.

A1 : P1 and A2 :zip P2 if
    (A1 A2) :zip (P1 P2).

A1 : P1 and (A2 A3) :zip (P2 P3) if
    (A1 A2 A3) :zip (P1 P2 P3).

A1 : P1 and (A2 A3 A4) :zip (P2 P3 P4) if
    (A1 A2 A3 A4) :zip (P1 P2 P3 P4).

A1 : P1 and (A2 A3 A4 A5) :zip (P2 P3 P4 P5) if
    (A1 A2 A3 A4 A5) :zip (P1 P2 P3 P4 P5).

// emission
show X if true X and X : D and emit D.
show (error X) if error X.

// sealing
Sealer knows Sealer if Sealer says (seal _).
Sealer knows Other if Sealer says (know Other).
X knows Z if X knows Y and Y knows Z.

error (Sealer sealed Domain but unknown Sayer said Atom) if
    and Sayer says (true Atom)
    and Sealer says (seal Domain)
    and Atom : Domain
    and not Sealer knows Sayer.

// subconsequence is now a fact.
true A if subconsequence and atom A. 

/////////// EXAMPLE /////////////

amy {
    defn likes (person person).
    decl person.
    true (likes ((person amy) (person bob))).

    true (person amy) if true (person dan).
    true (person dan).

    true (likes ((person rob) P)) if true P and P : person.

    emit person.
    emit likes.

    seal person.
    know bob.
}

bob {
    true (person ralph).
    decl liked.
    subconsequence.
    true (liked P) if true (likes (_ P)). 
    emit liked.
}