agreement {
    _ trusts main.

    error: Author infers (Truster trusts Thing)
    but Author is neither Truster nor agreement
        if Rule has_consequent (Truster trusts Thing)
        and not Rule has_pos_antecedent (Truster trusts Thing)
        and Rule has_author Author
        and diff {Truster Author agreement}.

    error: Author infers (Property Thing)
    but Author isnt trusted by Controller
        if Rule has_consequent (Property Thing)
        and Rule has_pos_antecedent (Property Thing)
        and Rule has_author Author
        and Controller controls Property
        and not Controller trusts Author.

    X trusts Z if X trusts Y and Y trusts Z.
}

amy { amy controls cool. amy trusts bob. }

# this is OK!
bob { bob trusts cho. }

# error! dan is untrusted but infers cool
dan { cool dan. }

# error! eli is neither amy nor main, so cannot infer (amy trusts _)
eli { amy trusts eli. cool eli. }

# error! fin is to blame for this! cannot make a 3rd party trusted either
fin { amy trusts eli. }

# nice try! (amy trusts gil) matches this!
gil { amy F gil if amy F bob. }
